/*
 * TCNJ NVX Switching module
 * Routes up to 32 inputs to 8 outputs with audio breakaway
 *
 * Programmer: Brian Bernas
 * bernasb@tcnj.edu
 * brianbernas@gmail.com
 *
 */

#SYMBOL_NAME "TCNJ NVX Router 32x8"

#CATEGORY "46" "TCNJ"

#DEFAULT_VOLATILE

#DEFINE_CONSTANT SOURCES	32 // Number of input sources
#DEFINE_CONSTANT DEST		8  // Number of destinations

// IO
Analog_Input	VideoRoute[DEST],
				_skip_,
				AudioRoute[DEST],
				_skip_;

String_Input	NVXStreamLocation[SOURCES][50],
				_skip_,
				NAXMulticastAddress[SOURCES][50],
				_skip_,
				InputName[SOURCES][24];

String_Output	NVXDecStreamLocation[DEST],
				_skip_,
				NAXDecRXMulticast[DEST],
				_skip_,
				DestInputName[DEST];

// Functions
Change VideoRoute
{
	Integer i; // Last modified array index

	i = GetLastModifiedArrayIndex();

	if (VideoRoute[i] = 0) {
		NVXDecStreamLocation[i] = "";
		DestInputName[i] = ""; // Clear the name string
		return;
	}
    
	// Bounds checking of the route
	if (VideoRoute[i] > 0 && VideoRoute[i] <= SOURCES) {
		NVXDecStreamLocation[i] = NVXStreamLocation[VideoRoute[i]];
		DestInputName[i] = InputName[VideoRoute[i]]; // Set the name string
	}
}

Change AudioRoute
{
	Integer i; // Last modified array index

	i = GetLastModifiedArrayIndex();

	if (AudioRoute[i] = 0) {
		NAXDecRXMulticast[i] = "";
		return;
	}
    
	// Bounds checking of the route
	if (AudioRoute[i] > 0 && AudioRoute[i] <= SOURCES) {
		NAXDecRXMulticast[i] = NAXMulticastAddress[AudioRoute[i]];
	}

}

Function main()
{
	WaitForInitializationComplete();
}
